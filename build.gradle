buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id "io.spring.dependency-management" version "0.5.4.RELEASE"
}

version "2.0.1"
group "org.grails.plugins"

apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "spring-boot"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "jacoco"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://nexus-bambooarg.rhcloud.com/nexus/content/groups/public/" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-logging'
    provided "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.springframework.boot:spring-boot-starter-tomcat"

    provided "org.grails:grails-web-boot"
    provided "org.grails:grails-dependencies"
    provided 'javax.servlet:javax.servlet-api:3.1.0'

    testCompile "org.grails:grails-plugin-testing"

    console "org.grails:grails-console"
}

jar {
    exclude "ar/com/orkodev/requestStat/metric/controllers/demo/**",
            "test/",
            "error.gsp",
            "index.gsp",
            "error404.gsp"
}

//jacoco config

jacoco {
    toolVersion = "0.7.1.201405082137"
}

jacocoTestReport {
    executionData test
}

jacocoTestReport.dependsOn test

grailsPublish {
    // TODO: Provide values here
    user = 'user'
    key = 'key'
    githubSlug = 'foo/bar'
    license {
        name = 'Apache-2.0'
    }
    title = "Grails App Request Stats Plugin"
    desc = "App request stats for Grails. Include stats of Hibernate access"
    developers = [orkonano: "Mariano Kfuri", maximicciu: "Maximiliano Micciullo"]
    portalUser = ""
    portalPassword = ""
}

//Publish
def bambooRepositoryUsername = System.getenv('BAMBOO_REPOSITORY_USERNAME') ?: project.hasProperty('bambooRepositoryUsername') ? project.bambooRepositoryUsername : ''
def bambooRepositoryPassword = System.getenv("BAMBOO_REPOSITORY_PASSWORD") ?: project.hasProperty('bambooRepositoryPassword') ? project.bambooRepositoryPassword : ''

publishing {
    repositories {
        maven {
            name "bambooRelease"
            credentials {
                username bambooRepositoryUsername
                password bambooRepositoryPassword
            }
            url "http://nexus-bambooarg.rhcloud.com/nexus/content/repositories/releases/"
        }

        maven {
            name "bambooSNAPSHOT"
            credentials {
                username bambooRepositoryUsername
                password bambooRepositoryPassword
            }
            url "http://nexus-bambooarg.rhcloud.com/nexus/content/repositories/snapshots/"
        }
    }
}